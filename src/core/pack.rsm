; Data Unpacking Routines -----------------------------------------------------
core_unpack_vram: ; HL = source, DE = target
    push    hl; store source address

    ; load uncompressed size from packed data
    ld      a,[hl]
    inc     hl
    ld      b,a

    ld      a,[hl]
    ld      c,a

    ; bc = de + size
    ld      h,d
    ld      l,e
    add     hl,bc
    ld      b,h
    ld      c,l

    pop     hl ; restore source address

    ; skip size word again
    inc     hl
    inc     hl

    call    core_unpack_raw_vram
    ret


core_unpack_raw_vram: ; HL = source, DE = target, BC = end (DE + size of data)

    ; check if we reached or passed the end of the data uncompressed data 
    ; (de >= bc)
    ld      a,d
    cp      b
    jp      nz,.next

    ; check low byte
    ld      a,e
    cp      c 
    ret     z ; c == e
    ret     nc ; c > e

.next:

    ; fetch next instruction byte
    ld      a,[hli]
    cp      127 ; a > 127
    jr      nc,.repeat

    ; copy X bytes
.copy:
    push    bc ; store end of data
    inc     a ; count++
    ld      b,a ; set up loop counter
    
.copy_loop:

    ; copy start
    di
    ld      a,[rSTAT]       ; <---+
    and     STATF_BUSY      ;     |
    jr      nz,@-4          ; ----+

    ld      a,[hli] ; load byte to copy
    ld      [de],a
    ei
    ; copy end

    inc     de
    dec     b
    jp      nz,.copy_loop

    ; next instruction
    pop     bc ; restore end of data
    jp      core_unpack_raw_vram


    ; repeat X bytes
.repeat:
    
    push    bc ; store end of data

    ; 255 - (count - minRun - 1) (minRun = 2)
    sub     3 ; count - 3
    cpl
    ;ld      b,a ; save count
    ;ld      a,255
    ;sub     b ; 255 - count, a is now the number of bytes to repeat
    ld      b,a ; set up loop counter

    ; load byte to repeat
    ld      a,[hli]
    ld      c,a
    
.repeat_loop:

    ; set start
    di
    ld      a,[rSTAT]       ; <---+
    and     STATF_BUSY      ;     |
    jr      nz,@-4          ; ----+

    ; while count > 0 inc de
    ld      a,c
    ld      [de],a
    ei
    ; set end

    inc     de
    dec     b
    jp      nz,.repeat_loop

    ; next instruction
    pop     bc ; restore end of data
    jp      core_unpack_raw_vram


