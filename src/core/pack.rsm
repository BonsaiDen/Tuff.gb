; Data Unpacking Routines -----------------------------------------------------
core_unpack_raw: ; HL = source, DE = target, BC = end (DE + size of data)

    ; check if we reached or passed the end of the data uncompressed data 
    ; (de >= bc)
    ld      a,d
    cp      b
    jp      nz,.next

    ; check low byte
    ld      a,e
    cp      c 
    jp      z,.done ; c == e
    jp      nc,.done ; c > e

.next:

    ; fetch next instruction byte
    ld      a,[hli]
    cp      127 ; a > 127
    jr      z,.copy
    jr      nc,.repeat

    ; copy X bytes
.copy:
    push    bc ; store end of data
    inc     a ; count++
    ld      b,a ; set up loop counter
    
.copy_loop:

    ; copy start
    ld      a,[rSTAT]       ; <---+
    and     STATF_BUSY      ;     |
    jr      nz,@-4          ; ----+

    ld      a,[hli] ; load byte to copy
    ld      [de],a
    ; copy end

    inc     de
    dec     b
    jp      nz,.copy_loop

    ; next instruction
    pop     bc ; restore end of data
    jp      core_unpack_raw


    ; repeat X bytes
.repeat:
    
    push    bc ; store end of data

    ; 255 - (count - minRun - 1) (minRun = 2)
    sub     3 ; count - 3
    cpl
    ;ld      b,a ; save count
    ;ld      a,255
    ;sub     b ; 255 - count, a is now the number of bytes to repeat
    ld      b,a ; set up loop counter

    ; load byte to repeat
    ld      a,[hli]
    ld      c,a
    
.repeat_loop:

    ; set start
    ld      a,[rSTAT]       ; <---+
    and     STATF_BUSY      ;     |
    jr      nz,@-4          ; ----+

    ; while count > 0 inc de
    ld      a,c
    ld      [de],a
    ; set end

    inc     de
    dec     b
    jp      nz,.repeat_loop

    ; next instruction
    pop     bc ; restore end of data
    jp      core_unpack_raw

.done:
    ret

