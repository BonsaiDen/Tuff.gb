; VBlank Handler --------------------------------------------------------------
core_vblank_handler:

    di
    push    af
    push    bc
    push    de
    push    hl

    ; Update palettes here so we avoid changing them midframe
    ld      a,[corePaletteBG]
    ld      [rBGP],a

    ld      a,[corePaletteSprite0]
    ld      [rOBP0],a

    ld      a,[corePaletteSprite1]
    ld      [rOBP1],a


    ; game specific code ------------------------------------------------------

    ; check if we need to load a new room if so set up the new entities
    ; before we copy to OAM
    ld      a,[mapRoomUpdate]
    cp      1
    call    z,entity_load

    ; call the DMA code for transfering sprite data to OAM
    call    $ff80 

    ; Set vblank flag, this will cause the core loop to run the game loop once
    ld      a,1
    ld      [coreVBlankDone],a

    ; update the player animation so the player sprites show the correct frames
    call    player_animation_update

    ; load the new room data into vram
    ld      a,[mapRoomUpdate]
    cp      1
    call    z,map_load_room

    ; end of game specific code -----------------------------------------------

    pop     hl
    pop     de
    pop     bc
    pop     af

    reti

