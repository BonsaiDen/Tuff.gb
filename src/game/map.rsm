SECTION "MapLogic",ROM0


; Map -------------------------------------------------------------------------
map_init: ; a = base value for background tiles

    sub     128; add offset for tile buffer at $9800
    ld      hl,mapRoomTileBuffer
    ld      bc,512
    call    core_mem_set

    ld      hl,$9800
    ld      bc,1024
    call    core_mem_set

    ret


; Scrolling -------------------------------------------------------------------
map_scroll_left:
    ld      a,[mapRoomX]
    ld      [mapRoomLastX],a
    dec     a
    ld      b,a
    ld      a,[mapRoomY]
    ld      [mapRoomLastY],a
    ld      c,a
    call    map_set_room
    ret


map_scroll_right:
    ld      a,[mapRoomX]
    ld      [mapRoomLastX],a
    inc     a
    ld      b,a
    ld      a,[mapRoomY]
    ld      [mapRoomLastY],a
    ld      c,a
    call    map_set_room
    ret


map_scroll_down:
    ld      a,[mapRoomX]
    ld      [mapRoomLastX],a
    ld      b,a
    ld      a,[mapRoomY]
    ld      [mapRoomLastY],a
    inc     a
    ld      c,a
    call    map_set_room
    ret


map_scroll_up:
    ld      a,[mapRoomX]
    ld      [mapRoomLastX],a
    ld      b,a
    ld      a,[mapRoomY]
    ld      [mapRoomLastY],a
    dec     a
    ld      c,a
    call    map_set_room
    ret


; Map -------------------------------------------------------------------------
map_set_room: ; b = x, c = y

    push    hl
    push    de
    push    bc

    ; store new room coordinates
    ld      a,b
    ld      [mapRoomX],a
    ld      a,c
    ld      [mapRoomY],a

    ; bank switch
    di
    ld      hl,$2000
    ld      [hl],MAP_ROOM_DATA_BANK

    ; RoomId = y * MAP_WIDTH + x
    ; RoomOffset = [MapOffset + RoomID * 2][0] and [1]
    ; MapData = (MapOffset + MapIndexSize + RoomOffset) - 1

    ; Offset into Index ------------------------

    ; y * MAP_WIDTH
    ld      h,16
    ld      e,c
    call    math_mul8b ; hl = offset into index

    ; + x
    ld      c,b
    ld      b,0
    add     hl,bc

    ; * 2
    add     hl,hl

    ; + MapOffset
    ld      bc,DataMapMain
    add     hl,bc

    ; Get Room Offset Value 
    ld      a,[hli]
    ld      b,a

    ld      a,[hl]
    ld      c,a

    ; MapOffset + Room Offset 
    ld      hl,DataMapMain
    add     hl,bc

    ; + MapIndexSize
    ld      bc,MAP_INDEX_SIZE
    add     hl,bc ; hl = pointer to packed room data

    ; unpack the data
    ld      de,mapRoomBlockBuffer
    ld      bc,mapRoomBlockBuffer + MAP_ROOM_SIZE + MAP_ENTITY_SIZE
    call    core_unpack_raw

    ; bank switch
    ld      hl,$2000
    ld      [hl],$01
    ei

    ; unload entities
    call    entity_store ; first store them
    call    entity_reset ; then reset them, as this will clear their active flag

    ; draw the room
    ld      a,1
    ld      [mapRoomUpdateRequired],a

    pop     bc
    pop     de
    pop     hl

    ret



; Core Map Draw Routine -------------------------------------------------------
map_load_room:
    
    ld      a,0
    ld      [mapRoomUpdateRequired],a
    ld      [mapFallableBlockCount],a

    ; target is the screen buffer
    ld      hl,$C400

    ; we read from the unpacked room data
    ld      de,mapRoomBlockBuffer - 1

    ; setup loop counts
    ld      b,8 ; row
    ld      c,0 ; col

.loop_y:

    ; y loop header
    ld      a,b
    cp      0
    jr      z,.done
    dec     b

    ; y loop body
    ld      c,10

.loop_x:

    dec     c ; reduce column counter

    ; RLE ----------------------------------------

    ; otherwise fetch the next block
    inc     de; 
    ld      a,[de]

.draw_block:

    ; draw 4 8x8 tiles
    push    hl
    push    de
    push    bc

    ; drawing ------------------------------------------
    
    ld      d,a ; save block data

    ; upper left
    ld      b,$40 ; block def row 0 offset
    ld      c,d
    ld      a,[bc] ; tile value
    sub     a,128 ; tile data offset
    ld      [hli],a ;  draw + 0


    ; upper right
    ld      b,$41 ; block def row 0 offset
    ld      c,d
    ld      a,[bc] ; tile value
    sub     a,128 ; tile data offset
    ld      [hl],a ; draw +1

    ; skip one screen buffer row
    ld      a,d ; save block
    ld      d,0
    ld      e,31
    add     hl,de
    ld      d,a ; restore block

    ; lower left
    ld      b,$42 ; block def row 0 offset
    ld      c,d
    ld      a,[bc] ; tile value
    sub     a,128 ; tile data offset
    ld      [hli],a ; draw + 32

    ; lower right
    ld      b,$43 ; block def row 0 offset
    ld      c,d
    ld      a,[bc] ; tile value
    sub     a,128 ; tile data offset
    ld      [hl],a ; draw + 33

    pop     bc; restore row / col

    ; check if fallable block -------------------------
    ld      a,d
    cp      MAP_FALLABLE_BLOCK_LIGHT
    jp      nz,.no_fallable_block


    ; get current index
    ld      a,[mapFallableBlockCount]
    cp      MAP_MAX_FALLABLE_BLOCKS
    jp      z,.no_fallable_block; maximum index reached skip

    ld      de,mapFallableBlocks
    ld      h,0
    ld      l,a
    add     hl,hl; x 2
    add     hl,hl; x 4
    add     hl,de; get offset address
    
    ; store tile type (dark / light) and reset active
    ld      a,%00000010; 7 bytes type, 1 bytes active
    ld      [hli],a

    ; reset frames 
    ld      a,0;
    ld      [hli],a

    ; store x and y coordinates
    ld      a,9
    sub     c
    ld      [hli],a; x / col
    ld      a,7
    sub     b
    ld      [hli],a; y / row

    ; next index
    ld      a,[mapFallableBlockCount]
    inc     a
    ld      [mapFallableBlockCount],a


    ; drawing ------------------------------------------
.no_fallable_block:
    pop     de
    pop     hl

    ; next x block
    inc     hl
    inc     hl

    ; x loop end
    ld      a,c
    cp      0
    jr      nz,.loop_x

    ; y loop end (skip one 16x16 screen data row)
    push    de
    ld      d,0
    ld      e,44 ; 12 left + one full row of 32
    add     hl,de
    pop     de

    jp      .loop_y


.done:
    ld      hl,$C400
    ld      de,$9800
    ld      bc,512
    call    core_vram_cpy_vblank

    ret


; Collision Detection ---------------------------------------------------------
map_get_collision: ; b = x pos, c = y pos (both without scroll offsets) -> a = 1 if collision, 0 = no collision

    ; check for the bottom end of the screen
    ; if we index into the ram beyond this area we will read invalid data
    ; so we assume that there is never any collision beyond y 128
    ld      a,c
    cp      128
    jr      nc,.off_screen ; reset collision flag and indicate no collision

    ; divide x by 8
    srl     b
    srl     b
    srl     b

    ; divide y by 8
    srl     c
    srl     c
    srl     c

    push    hl

    ; get offset into collision table
    call    map_get_tile_value
    ld      h,DataTileCol >> 8; high byte, needs to be aligned at 256 bytes
    ld      l,a
    ld      a,[hl]

    pop     hl

    ; check type of collision
    cp      1; normal blocks
    jp      z,.collision
    cp      5; breakable
    jp      z,.collision

    ; everything that is not solid has no collision
.no_collision:
    ld      [mapCollisionFlag],a
    ld      a,0
    ret

.collision:
    ld      [mapCollisionFlag],a
    ld      a,1
    ret

.off_screen:
    ld      a,0
    ld      [mapCollisionFlag],a
    ret


map_get_tile_collision:
    push    bc
    push    hl

    ; get offset into collision table
    call    map_get_tile_value
    ld      hl,DataTileCol ; needs to be aligned at 256 bytes
    ld      l,a
    ld      a,[hl]

    pop     hl
    pop     bc
    ret


map_get_tile_value: ; b = tile x, c = tile y -> a = value
    ; gets the tile value from the room data buffer (not VRAM!)
    ; trashes hl and bc

    ld      a,b ; store x

    ; y * 32
    ld      h,0 
    ld      l,c

    add     hl,hl ; 2
    add     hl,hl ; 4
    add     hl,hl ; 8
    add     hl,hl ; 16
    add     hl,hl ; 32

    ; + mapRoomTileBuffer + x
    ld      b,mapRoomTileBuffer >> 8; high byte, needs to be aligned at 256 bytes
    ld      c,a ; restore x
    add     hl,bc

    ; load tile value from background buffer
    ld      a,[hl]
    sub     128 ; convert into 0-255 range

    ret


map_set_tile_value: ; b = tile x, c = tile y, a = value
    ; sets the tile value in the room data buffer AND in vram

    push    af
    push    bc
    push    hl
    push    de

    ; convert tile into -127-128 range and store into d
    add     128
    ld      d,a

    ; y * 32
    ld      h,0 
    ld      l,c

    add     hl,hl ; 2
    add     hl,hl ; 4
    add     hl,hl ; 8
    add     hl,hl ; 16
    add     hl,hl ; 32

    ld      a,b ; store x
    push    hl
    push    af

    ; + mapRoomTileBuffer + x
    ld      b,mapRoomTileBuffer >> 8; high byte, needs to be aligned at 256 bytes
    ld      c,a ; restore x
    add     hl,bc
    ld      a,d
    ld      [hl],a; set tile in buffer

    pop     af
    pop     hl

    ; + VRAM buffer + x
    ld      b,$98
    ld      c,a ; restore x
    add     hl,bc

    ; store tile value to background buffer
    ld      a,[rSTAT]       ; <---+
    and     STATF_BUSY      ;     |
    jr      nz,@-4          ; ----+
    ld      a,d
    ld      [hl],a

    pop     de
    pop     hl
    pop     bc
    pop     af

    ret


; Animation -------------------------------------------------------------------
map_animate_tiles:

    ; check if there are any tile animations
    ld      a,TILE_ANIMATION_COUNT
    cp      0
    ret     z

    ; store state
    push    hl
    push    de
    push    bc

    ; loop and animate tiles
    ld      d,0
    ld      hl,mapAnimationIndexes
.animate:

    ; TODO check what animations are used in the current room and limit
    ; the updates?
    push    hl
    push    de

    ; load current delay value
    ld      hl,mapAnimationDelay
    ld      e,d
    ld      d,0
    add     hl,de
    ld      a,[hl] ; delay for the current tile

    ; check if we hit zero delay
    cp      0
    jp      nz,.delay ; if not decrease delay and animate the tile

    ; otherwise reset the delay and animate
    pop     de
    push    de
    push    hl
    ld      hl,DataTileAnimationDelay
    ld      e,d
    ld      d,0
    add     hl,de
    ld      a,[hl]
    pop     hl

    ; restore delay
    ld      [hl],a

    pop     de
    pop     hl

    ; update current tile animation index
    ld      a,[hl]
    inc     a
    and     %00000011 ; modulo 4
    ld      [hl],a

    ; update vram  (hl = source, de = dest, bc = size (16))
    push    hl
    push    de

    ; store animation index
    ld      e,a 

    ; get base tile value of the animation (base + d * 4)
    ld      hl,DataTileAnimation
    ld      b,0
    ld      c,d
    sla     c
    sla     c
    add     hl,bc
    ld      a,[hl] 
    ld      d,a ; base tile value $00 - $ff

    ; get the current tile value of the animation (base + d * 4 + a)
    ld      c,e 
    add     hl,bc
    ld      a,[hl] ; current tile value $00 - $ff

    ; get the target address in vram (multiply the base tile by 16 + $8800)
    ld      h,0
    ld      l,d
    add     hl,hl
    add     hl,hl
    add     hl,hl
    add     hl,hl
    ld      bc,$8800
    add     hl,bc
    ld      d,h
    ld      e,l

    ; get the current animation address (multiply the current (tile - $c0) by 16 + DataTileImg)
    sub     $c0 ; correct offset into ram buffer
    ld      h,0
    ld      l,a
    add     hl,hl
    add     hl,hl
    add     hl,hl
    add     hl,hl
    ld      bc,TILE_ANIMATION_BUFFER
    add     hl,bc

    ; set the length
    ld      b,0
    ld      c,16
    call    core_vram_cpy
    jp      .next

.delay:
    dec     a
    ld      [hl],a

.next:
    pop     de
    pop     hl
    ; end of vram update

    ; next animation
    inc     hl
    inc     d
    ld      a,d
    cp      TILE_ANIMATION_COUNT
    jp      nz,.animate
    ; end of loop

    ; restore state
    pop     bc
    pop     de
    pop     hl

    ret


; Breakable Blocks ------------------------------------------------------------
map_check_breakable_block_top:

    ; break the four 8x8 blocks
    push    bc
    sla     b; convert into 8x8 index
    sla     c; convert into 8x8 index

    ; check if we actually need to break them
    call    map_get_tile_collision
    cp      5
    jp      nz,.no

    ld      a,1
    pop     bc
    ret

.no:
    ld      a,0
    pop     bc
    ret


map_check_breakable_block_bottom:

    ; break the four 8x8 blocks
    push    bc
    sla     b; convert into 8x8 index
    sla     c; convert into 8x8 index
    inc     c

    ; check if we actually need to break them
    call    map_get_tile_collision
    cp      5
    jp      nz,.no

    ld      a,1
    pop     bc
    ret

.no:
    ld      a,0
    pop     bc
    ret


map_get_block_value: ; b = x, c = y -> a block value

    push    bc
    push    hl
    push    de

    ; y * 10
    ld      h,10
    ld      e,c
    call    math_mul8b

    ; add x
    ld      c,b
    ld      b,0
    add     hl,bc

    ; add base offset
    ld      de,mapRoomBlockBuffer
    add     hl,de

    ld      a,[hl]

    pop     de
    pop     hl
    pop     bc

    ret


map_destroy_breakable_block_top: ; b = block x, c = block y -> a = broken or not

    ; break the four 8x8 blocks
    push    bc
    
    ; get the 16x16 block tile
    call    map_get_block_value
    sla     b; convert into 8x8 index
    sla     c; convert into 8x8 index
    cp      MAP_BREAKABLE_BLOCK_LIGHT
    jp      z,.light

.dark:
    ld      a,$70
    call    map_check_breakable_surrounding
    call    map_set_tile_value; top left
    inc     b

    ld      a,$71
    call    map_check_breakable_surrounding
    call    map_set_tile_value; top right
    pop     bc
    ret

.light:
    ld      a,MAP_BACKGROUND_TILE_LIGHT
    call    map_set_tile_value; top left
    inc     b
    call    map_set_tile_value; top right
    pop     bc
    ret


map_destroy_breakable_block_bottom: ; a = block x, c = block y -> a = broken or not

    ; break the four 8x8 blocks
    push    bc

    ; get the 16x16 block tile
    call    map_get_block_value
    sla     b; convert into 8x8 index
    sla     c; convert into 8x8 index
    inc     c; lower row
    cp      MAP_BREAKABLE_BLOCK_LIGHT
    jp      z,.light

.dark:
    ld      a,$72
    call    map_check_breakable_surrounding
    call    map_set_tile_value; bottom left
    inc     b

    ld      a,$73
    call    map_check_breakable_surrounding
    call    map_set_tile_value; bottom right
    pop     bc
    ret

.light:
    ld      a,MAP_BACKGROUND_TILE_LIGHT
    call    map_set_tile_value; bottom left
    inc     b
    call    map_set_tile_value; bottom right
    pop     bc
    ret


map_check_breakable_surrounding: ; b = tx, c = ty
    push    hl
    push    af

.left:
    ld      a,b; ignore blocks < 0
    cp      0
    jp      z,.top
    push    bc
    dec     b
    call    map_get_tile_value
    pop     bc
    
    cp      MAP_BACKGROUND_TILE_LIGHT
    jp      z,.found_left

.top:
    ld      a,c; ignore blocks < 0
    cp      0
    jp      z,.right
    push    bc
    dec     c
    call    map_get_tile_value
    pop     bc

    cp      MAP_BACKGROUND_TILE_LIGHT
    jp      z,.found_top

.right:

    ld      a,b; ignore blocks > 20
    cp      20
    jp      z,.bottom
    push    bc
    inc     b
    call    map_get_tile_value
    pop     bc

    cp      MAP_BACKGROUND_TILE_LIGHT
    jp      z,.found_right

.bottom:

    ld      a,b; ignore blocks > 8
    cp      8
    jp      z,.found_none
    push    bc
    inc     c
    call    map_get_tile_value
    pop     bc

    cp      MAP_BACKGROUND_TILE_LIGHT
    jp      z,.found_bottom

.found_none:
    pop     af
    pop     hl
    ret

.found_left:
    pop     af
    ld      a,MAP_BACKGROUND_FADE_LEFT
    pop     hl
    ret

.found_top:
    pop     af
    ld      a,MAP_BACKGROUND_FADE_TOP
    pop     hl
    ret

.found_right:
    pop     af
    ld      a,MAP_BACKGROUND_FADE_RIGHT
    pop     hl
    ret

.found_bottom:
    pop     af
    ld      a,MAP_BACKGROUND_FADE_BOTTOM
    pop     hl
    ret


; Fallable blocks -------------------------------------------------------------
map_check_fallable_blocks:
    ld      a,[mapFallableBlockCount]
    cp      0
    ret     z

    ; setup loop counter
    ld      a,0
    ld      b,a

.loop:

    ; get offset
    ld      de,mapFallableBlocks
    ld      h,0
    ld      l,b
    add     hl,hl; x 2
    add     hl,hl; x 4
    add     hl,de; get offset address

    ; check if inactive
    jp      .check_active
.check_active:
    ld      a,[hli]
    and     %00000001
    cp      1
    jp      z,.active
            
    ; skip frame
    inc     hl

    ; load block x coordinate
    ld      a,[hli]
    ld      c,a

    ; load player coordinates and convert into blocks
    ld      a,[playerX]
    srl     a; divide by 16
    srl     a; divide by 16
    srl     a; divide by 16
    srl     a; divide by 16
    cp      c
    jp      z,.found_x

    ld      a,[playerX]
    add     PLAYER_HALF_WIDTH - 3
    srl     a; divide by 16
    srl     a; divide by 16
    srl     a; divide by 16
    srl     a; divide by 16
    cp      c
    jp      z,.found_x

    ld      a,[playerX]
    sub     PLAYER_HALF_WIDTH - 2
    srl     a; divide by 16
    srl     a; divide by 16
    srl     a; divide by 16
    srl     a; divide by 16
    cp      c
    jp      z,.found_x

    jp      .active

    ; load block y coordinate
.found_x:
    ld      a,[hl]
    ld      c,a

    ; check the block 2 pixel under the player
    ; TODO check if in upper half of block only ?
    ld      a,[playerY]
    add     1
    srl     a; divide by 16
    srl     a; divide by 16
    srl     a; divide by 16
    srl     a; divide by 16
    cp      c
    jp      nz,.active

    dec     hl
    dec     hl
    dec     hl

    ; if near set active
    ld      a,[hl]
    or      %00000001; type / active flag
    ld      [hli],a

    call    map_update_falling_block

    ; play sound
    ld      a,SOUND_MAP_FALLING_BLOCK
    call    sound_play

    ; loop
.active:
    inc     b
    ld      a,[mapFallableBlockCount]
    cp      b
    jp      nz,.loop
    ret


map_update_falling_blocks:
    ld      a,[mapFallableBlockCount]
    cp      0
    ret     z

    ; setup loop counter
    ld      a,0
    ld      b,a

.loop:

    ; get offset
    ld      de,mapFallableBlocks
    ld      h,0
    ld      l,b
    add     hl,hl; x 2
    add     hl,hl; x 4
    add     hl,de; get offset address

    ; check if active
    ld      a,[hli]
    and     %00000001
    cp      1
    jp      nz,.inactive

    call    map_update_falling_block

    ; loop
.inactive:
    inc     b
    ld      a,[mapFallableBlockCount]
    cp      b
    jp      nz,.loop
    ret


map_update_falling_block: ; b = index

    ; check frame count
    ld      a,[hl]
    cp      4
    ret     z

    ; next frame
    ld      a,[hl]
    inc     a
    ld      [hl],a

    ; store loop counter and frame pointer 
    push    bc
    push    hl

    ; skip frame count
    ld      a,[hl]
    ld      d,a; store frame count
    inc     hl

    ; load x / y coordinates
    ld      a,[hli]; load x
    ld      b,a
    ld      a,[hl]; load y
    ld      c,a
    sla     b; convert into 8x8 index
    sla     c; convert into 8x8 index

    ; check tile color
.light:
    ld      a,d
    cp      4
    jp      z,.done_light
    ld      a,MAP_FALLING_TILE_LIGHT
    add     d
    ld      e,a
    add     4
    jp      .set

.done_light:
    ld      a,MAP_BACKGROUND_TILE_LIGHT
    ld      e,a

.set:
    call    map_set_tile_value
    inc     b
    ld      a,e
    call    map_set_tile_value

    ; restore frame pointer
    pop     hl
    pop     bc

.done:
    ret

