; Initialization --------------------------------------------------------------
game_init:

    ; setup background tile palette
    ld      a,%11100100
    ld      [rBGP],a; load a into the memory pointed to by rBGP

    ; set object palette 0
    ld      a,%11010000 
    ld      [rOBP0],a 

    ; reset fading
    ld      a,0
    ld      [gameFadeMode],a

    ; load char data
    ld      hl,DataCharImg
    ld      de,$8000
    ld      bc,2048     ; length (8 bytes per tile) x (256 tiles)
    call    mem_Copy    ; Copy tile data to memory

    ; load tile data
    ld      hl,DataTileImg
    ld      de,$8800
    ld      bc,4096     ; length (8 bytes per tile) x (256 tiles)
    call    mem_Copy    ; Copy tile data to memory

    ; Player
    call    player_init

    ; Map
    ld      b,2
    ld      c,0
    call    map_set_room

    ; Initial Spawn Location
    call    save_store_player

    ; Sound
    ld      hl,$FF25
    ld      [hl],%11111111 ; output all channels to both speakers
    inc     hl
    ld      [hl],%00000000 ; dis-able sound

    ;ld      hl,$FF10

    ;;          -SSSOIII / sweep register
    ;ld      a,%00000000
    ;ld      [hli],a

    ;;          WWLLLLLL / length wave pattern duty
    ;ld      a,%10111111
    ;ld      [hli],a

    ;;          VVVVENNN
    ;ld      a,%01110001
    ;ld      [hli],a

    ;;          FFFFFFFF frequency lower 8 bits
    ;ld      a,%00000010
    ;ld      [hli],a
    ;
    ;;          IS000FFF frequency lower 8 bits
    ;ld      a,%10000000
    ;ld      [hli],a


    ; noise
    ;ld      hl,$FF20
    ;ld      [hl],%00001111
    ;inc     hl

    ;;             VVVVESSS
    ;ld      [hl],%00010000 ; fade out
    ;inc     hl

    ;;             FFFFSDDD
    ;ld      [hl],%01110101 ; polynomial counter, higher value > lower noise (darker), noise freq, 1 = 7 steps, sounds glichted, 0 = 15 = normal noise
    ;inc     hl
    ;;             IE
    ;ld      [hl],%10000000 ; start intiially, play it endlessly


    ret


; Main Loop -------------------------------------------------------------------
game_loop_half:
    call    player_update
    ret


game_loop:
    call    sprite_animate_all

    ; Fading test
    ld      a,[inputState]
    and     %00000010
    jp      z,.not_fade_out
    call    game_flash_screen

.not_fade_out:
    ld      a,[inputState]
    and     %00000100
    jp      z,.not_fade_in
    call    game_fade_in

.not_fade_in:
    ret


; Screen Fading ---------------------------------------------------------------
gameBGFadeMask:
    DB      %11100100
    DB      %11100101
    DB      %11101010
    DB      %11111111

gameSpriteFadeMask:
    DB      %11010000 
    DB      %11100100 
    DB      %11101000 
    DB      %11111100 


game_fade_out:
    ld      a,1
    ld      [gameFadeMode],a
    ld      a,0
    ld      [gameFadeIndex],a
    ret


game_fade_in:
    ld      a,2
    ld      [gameFadeMode],a
    ld      a,3
    ld      [gameFadeIndex],a
    ret


game_fade_timer:

    ; check if fading
    ld      a,[gameFadeMode]
    cp      0
    ret     z

    ; fade index
    ld      a,[gameFadeIndex]
    ld      b,0
    ld      c,a
    
    ; bg mask
    ld      hl,gameBGFadeMask
    add     hl,bc
    ld      a,[hl]
    ld      [rBGP],a

    ; sprite mask
    ld      hl,gameSpriteFadeMask
    add     hl,bc
    ld      a,[hl]
    ld      [rOBP0],a

    ; fade mode
    ld      a,[gameFadeMode]
    cp      2
    jp      z,.fade_in
        
    ; fade out
    ld      a,[gameFadeIndex]
    cp      3
    jp      z,.complete
    inc     a
    ld      [gameFadeIndex],a
    ret

    ; fade in
.fade_in:
    ld      a,[gameFadeIndex]
    cp      0
    jp      z,.complete
    dec     a
    ld      [gameFadeIndex],a
    ret

.complete:
    ld      a,0
    ld      [gameFadeMode],a
    ret


; Screen Flashing -------------------------------------------------------------
gameBGFlashMask:
    DB      %00000000
    DB      %00000000
    DB      %01010100
    DB      %10100100
    DB      %11100100


gameSpriteFlashMask:
    DB      %00000000
    DB      %00000000
    DB      %01010000 
    DB      %10010000 
    DB      %11010000 


game_flash_screen:
    ld      a,1
    ld      [gameFlashMode],a
    ld      a,0
    ld      [gameFlashIndex],a
    call    game_flash_timer
    ret


game_flash_timer:

    ; check if fading
    ld      a,[gameFlashMode]
    cp      0
    ret     z

    ; flash index
    ld      a,[gameFlashIndex]
    ld      b,0
    ld      c,a
    
    ; bg mask
    ld      hl,gameBGFlashMask
    add     hl,bc
    ld      a,[hl]
    ld      [rBGP],a

    ; sprite mask
    ld      hl,gameSpriteFlashMask
    add     hl,bc
    ld      a,[hl]
    ld      [rOBP0],a

    ; update
    ld      a,[gameFlashIndex]
    cp      4
    jp      z,.complete
    inc     a
    ld      [gameFlashIndex],a
    ret

.complete:
    ld      a,0
    ld      [gameFlashMode],a
    ret



; 125ms Timer -----------------------------------------------------------------
game_timer_125
    call    map_animate_tiles
    call    player_timer_125
    call    game_fade_timer
    call    game_flash_timer
    ret

